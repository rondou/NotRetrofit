buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.novoda:bintray-release:0.3.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':retrofit')
    compile 'com.android.support:support-annotations:22.2.0'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'javax.inject:javax.inject:1'

    // testing
    testCompile 'junit:junit-dep:4.10'
    testCompile 'org.mockito:mockito-core:1.8.5'
    testCompile('org.robolectric:robolectric:2.3') {
      exclude group: 'com.android.support'
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
    }
}

apply plugin: 'com.github.dcendents.android-maven'
apply from: 'android-javadoc.gradle'
group = 'com.github.yongjhih'

apply plugin: 'com.novoda.bintray-release'

def getRepositoryUsername() {
    return hasProperty('BINTRAY_USER') ? BINTRAY_USER : ""
}

def getRepositoryPassword() {
    return hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ""
}

publish {
    userOrg = 'yongjhih'
    groupId = 'com.infstory'
    artifactId = 'retrofit-android'
    //uploadName = ''
    publishVersion = '2.0.5'
    desc = 'Type-safe REST client for Android and Java'
    website = 'https://github.com/yongjhih/NotRetrofit'
    bintrayUser = getRepositoryUsername()
    bintrayKey = getRepositoryPassword()
    dryRun = false
}
